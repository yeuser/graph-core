buildscript {
    ext.kotlin_version = '1.3.72'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
}

group = 'me.graph'
version = '0.2.0'

ext {
    java_src_version = 1.8
    java_dst_version = 1.8
}

sourceCompatibility = 11
targetCompatibility = 1.8

allprojects {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

repositories {
    jcenter()
    mavenCentral()
}

configurations {
    ktlint
}

test {
    useJUnitPlatform()
}

dependencies {
    ktlint "com.pinterest:ktlint:0.36.0"

    implementation group: 'com.google.guava', name: 'guava', version: '26.0-jre'
//    implementation group: 'com.carrotsearch', name: 'hppc', version: '0.8.1'
//    implementation group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'
    implementation group: 'it.unimi.dsi', name: 'fastutil', version: '8.2.2'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    testImplementation "org.jetbrains.kotlin:kotlin-reflect"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.1.0-alpha"

    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "io.kotest:kotest-runner-junit5-jvm:4.0.1" // for kotest framework
    testImplementation "io.kotest:kotest-assertions-core-jvm:4.0.1" // for kotest core jvm assertions
    testImplementation "org.junit.jupiter:junit-jupiter:5.6.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "--experimental", "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "--experimental", "-F", "src/**/*.kt"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = java_dst_version
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = java_dst_version
    }
}

jar {
    doFirst {
        manifest {
            attributes(
                    "Implementation-Title": "${project.name}",
                    "Implementation-Version": archiveVersion
            )
        }
    }
}

manifest {
    attributes 'provider': 'gradle'
}
configurations {
    published
}
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    published sourceJar
    published javadocJar
}